// returns a single frame of the animation
frame = {n ->

  // message to display
  message = "                HELLO WORLD                "

  // the visible part of the message, as a list
  message = (map (range n) {item i -> <- (get message i)})

  // convert back to string
  message = (reduce message {acc item i -> <- (join acc item)} "")

  // add car
  <- (join message "ō͡≡o")
}

// init
// \e[H goes to top left
// \e[2J clears screen
// \e[?25l disables cursor
(print "\e[H\e[2J\e[?25l")

// render loop
(until "" {state n ->

  // calculate frame number
  frame_number = (add (remainder n 40) 1)

  // clear when animation loop
  (if (is frame_number 1) {(print "\e[H\e[J")})

  // \e[H goes to top left
  (print (join "\e[H" "Frame: " (string frame_number) "\n\n"))

  // get color escape code
  color_code = (join "\e[" (string (add 31 (remainder frame_number 7))) "m")

  // print frame
  (print (join color_code (frame frame_number) "\n"))

  // print floor
  // \e[0m resets color
  (print "\e[0m‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾\n")

  // wait to slow down animation
  (wait 0.1)
})